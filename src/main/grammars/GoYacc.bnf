{
    parserClass="idea.goyacc.ast.GoYaccParser"
    generate=[element-case="lower"]

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="GoYacc"
    psiPackage="idea.goyacc.psi"
    psiImplClassSuffix="Impl"
    psiImplPackage="idea.goyacc.psi.impl"
    //psiImplUtilClass="idea.goyacc.psi.GoYaccPsiUtil"

    elementTypeHolderClass="idea.goyacc.psi.GoYaccType"
    elementTypeFactory="idea.goyacc.psi.GoYaccTypeFactory.newElementType"
    tokenTypeFactory="idea.goyacc.psi.GoYaccTypeFactory.newTokenType"

    tokens=[GO_CODE]
}


spec ::= decl_list rule_list tail_code?


decl_list ::= (token_decl | prec_decl | type_decl | start_decl | union_decl | code_decl | misc_decl)*

field ::= '<' IDENT '>' {pin=1 recoverWhile=type_recover}
private type_recover ::= !(IDENT)

private prec_word ::= LEFT | RIGHT | NONASSOC | PRECEDENCE
private decl_word ::= (prec_word | TOKEN | TYPE | START | UNION | LCURL | ERROR_VERBOSE)
private decl_recover ::= !(decl_word | START_RULES)

token_decl ::= TOKEN field? token_list {pin=1 recoverWhile=decl_recover}
prec_decl  ::= prec_word field? token_list {pin=1 recoverWhile=decl_recover}
type_decl  ::= TYPE field symbol_list {pin=1 recoverWhile=decl_recover}
union_decl ::= UNION BEGIN GO_CODE? END {pin=1 recoverWhile=decl_recover}
start_decl ::= START IDENT {pin=1 recoverWhile=decl_recover}
code_decl  ::= LCURL GO_CODE? RCURL {pin=1 recoverWhile=decl_recover}
misc_decl  ::= ERROR_VERBOSE {recoverWhile=decl_recover}

private token_list ::= token+ | token (',' token)*
token ::= IDENT NUM_LIT? STR_LIT? {pin=1 recoverWhile=token_recover}
private token_recover ::= !(',' | IDENT | decl_word | START_RULES)

private symbol_list ::= symbol+ | symbol (',' symbol)*
symbol ::= IDENT STR_LIT? {pin=1 recoverWhile=symbol_recover}
private symbol_recover ::= !(',' | IDENT | decl_word | START_RULES)


rule_list ::= START_RULES rule+ {pin=1}

rule ::= C_IDENT ':' rule_alt ('|' rule_alt)* {pin=2 recoverWhile=rule_recover}
rule_alt ::= rule_elem* rule_prec? ';'? {recoverWhile=rule_alt_recover}
rule_elem ::= IDENT | STR_LIT | action
rule_prec ::= PREC IDENT action? {pin=1}
action ::= BEGIN GO_CODE END {pin=1 recoverWhile=action_recover}

private rule_recover ::= !(C_IDENT | START_TAIL)
private rule_alt_recover ::= !('|' | C_IDENT | START_TAIL)
private action_recover ::= !(';' | '|' | BEGIN | STR_LIT | IDENT | C_IDENT | PREC | START_TAIL)


tail_code ::= START_TAIL GO_CODE? {pin=1}
